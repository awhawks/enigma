
buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "eu.appsatori:gradle-fatjar-plugin:0.2-rc1"
        classpath "net.sf.proguard:proguard-gradle:5.0"
    }
}

apply plugin: "java"
apply plugin: "eclipse"
apply plugin: "maven"
apply plugin: "fatjar"

sourceCompatibility = 1.7
targetCompatibility = 1.7

group = "com.cuchazinteractive"
archivesBaseName = "enigma"
version = "0.5.1b"

sourceSets {
	main {
		java {
			srcDir "src"
		}
		resources {
			srcDir "conf"
		}
	}
	test {
		java {
			srcDir "test"
		}
	}
}

repositories {
	mavenCentral()
}

dependencies {
	compile fileTree( dir: "libs", include: "*.jar" )
	compile "de.sciss:jsyntaxpane:1.0.0"
	compile "com.google.guava:guava:17.0"
	compile "org.javassist:javassist:3.18.1-GA"
	
	testCompile "junit:junit:4.11"
	testCompile "org.hamcrest:hamcrest-all:1.3"
}

fatJar {
	from( "." ) {
		include( "*.txt" )
	}
	manifest {
		attributes(
			"Title": archivesBaseName,
			"Manifest-Version": "1.0",
			"Version": version,
			"Main-Class" : "cuchaz.enigma.Main"
		)
	}
}

task jarLoneClass( type: Jar ) {
	from( sourceSets.test.output ) {
		include( "cuchaz/enigma/inputs/Keep.class" )
		include( "cuchaz/enigma/inputs/loneClass/**" )
	}
	archiveName( "testLoneClass.jar" )
}

task obfTestCases( type: proguard.gradle.ProGuardTask ) {
	dependsOn jarLoneClass
	
	injars( "build/libs/testLoneClass.jar" )
	outjars( "build/libs/testLoneClass.obf.jar" )
	
	libraryjars( "${System.getProperty('java.home')}/lib/rt.jar" )
	overloadaggressively
	repackageclasses
	allowaccessmodification
	dontoptimize
	
	keep( "class cuchaz.enigma.inputs.Keep" )
	dontshrink
}